import groovy.json.JsonSlurper

import java.nio.file.Files

plugins {
    id 'com.enonic.xp.app' version '1.0.17'
    id "com.moowork.node" version "1.1.1"
}

app {
    name = project.appName
    displayName = 'React Starter Kit'
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
    systemVersion = "${xpVersion}"
}

node {
    download = true
    version = '6.9.2'
}


dependencies {
    include "com.enonic.xp:lib-repo:${xpVersion}"
    include "com.enonic.xp:lib-node:${xpVersion}"
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include "com.enonic.xp:lib-mustache:${xpVersion}"
    include "com.enonic.xp:lib-thymeleaf:${xpVersion}"

    include "com.enonic.lib:lib-router:1.0.1"
    include 'com.enonic.lib:util:1.3.0'
    include 'com.enonic.lib:lib-cache:1.1.0'

    include group: 'org.jdom', name: 'jdom', version: '2.0.2'
    include group: 'org.json', name: 'json', version: '20180813'
    include group: 'jaxen', name: 'jaxen', version: '1.1.6'
}

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
    maven {
        url 'http://repo.enonic.com/public'
    }
}



npmInstall.inputs.file("package.json")
npmInstall.inputs.file("package-lock.json")
npmInstall.outputs.dir("node_modules")


// Transpile .js and .es6 files under src/main/resources.
// These are "pure XP" source code files, not JS (and react) assets. Those must be transpiled separately, by the webpackReact4xp task below.
// This is currently done by reserving .es6 and .js files below src/main/resources/ for this task,
// while reserving .JSX files under that, and all relevant extensions in other locations, for the webpackReact4xp task.
task babelXP(type: NodeTask) {
    script = file('node_modules/babel-cli/bin/babel.js')
    args = ["src/main/resources", "--out-dir", "build/resources/main", "--ignore", "**/*.jsx"]

    inputs.dir 'src/main/resources'
    outputs.dir("build/resources/main")
}
babelXP.dependsOn += 'npmInstall'
babelXP.dependsOn += 'processResources'

def ROOT = new File(".").absolutePath

// Necessary placeholders, will be filled during
def REACT4XP_CONFIG = {}

def REACT4XP_CONFIG_FILE = "src/react4xp_constants.json"

// Add override values to taste:
def REACT4XP_OVERRIDES = '{"R4X_TARGETSUBDIR":"react4xp_testcustombuildtarget", "overwriteConstantsFile":true}' //''{"verbose": true}'


task config_react4xp(type: NodeTask) {
    script = file('node_modules/react4xp-buildconstants/lib/cli.js')
    args = [
        ROOT,
        ROOT + "/" + REACT4XP_CONFIG_FILE,
        REACT4XP_OVERRIDES
    ]

    // After the script has run and created the config JSON file, some finalizing operations:
    doLast {
        // Read the file content into an object
        def configFile = new File(REACT4XP_CONFIG_FILE)
        REACT4XP_CONFIG = new JsonSlurper().parseText(configFile.text)
        //print("Config from "+ REACT4XP_CONFIG_FILE)
        //print(REACT4XP_CONFIG)

        // Update the configuration of the next tasks, using the constructed values from the config file:
        tasks['webpack_react4xp_core'].configure {
            inputs.dir(REACT4XP_CONFIG.SRC_R4X)
            outputs.dir(REACT4XP_CONFIG.BUILD_R4X)
        }
        tasks['webpack_react4xp_components'].configure {
            inputs.dir(REACT4XP_CONFIG.SRC_SITE)
            inputs.dir(REACT4XP_CONFIG.SRC_R4X)
            outputs.dir(REACT4XP_CONFIG.BUILD_R4X)
        }

        /*
        // Copy the config file itself into the target react4xp build folder, as defined in the config file:
        def fileName = configFile.getName();
        File directory = new File(REACT4XP_CONFIG.BUILD_R4X);
        if (! directory.exists()) {
            directory.mkdir();
        }
        new File(REACT4XP_CONFIG.BUILD_R4X + "/" + fileName) << configFile.text
        //*/
    }
}
config_react4xp.dependsOn += 'npmInstall'


// TODO: the webpack scripts below can maybe now be put into NPM and run directly from i.e. "node_modules/reac4xp/"?
task webpack_react4xp_core(type: NodeTask) {
    script = file('node_modules/webpack/bin/webpack.js')
    args = [
        '--config', 'webpack.config.react4xp.externals.js', 
        '--config', 'webpack.config.react4xp.nashornPolyfills.js', 
        '--config', 'webpack.config.react4xp.core.js',
        '--env.REACT4XP_CONFIG_FILE=' + ROOT + '/' + REACT4XP_CONFIG_FILE,
        '--progress', '--color'
    ]
    inputs.file("webpack.config.react4xp.externals.js")
    inputs.file("webpack.config.react4xp.nashornPolyfills.js")
    inputs.file("webpack.config.react4xp.core.js")

    inputs.file(REACT4XP_CONFIG_FILE)
    inputs.file("package.json")
    inputs.file("package-lock.json")
}
webpack_react4xp_core.dependsOn += 'config_react4xp'


task webpack_react4xp_components(type: NodeTask) {
    script = file('node_modules/webpack/bin/webpack.js')
    args = [
        '--config', 'webpack.config.react4xp.js',
        '--env.REACT4XP_CONFIG_FILE=' + ROOT + '/' + REACT4XP_CONFIG_FILE,
        '--progress', '--color'
    ]

    inputs.file("webpack.config.react4xp.js")

    inputs.file(REACT4XP_CONFIG_FILE)
    inputs.file("package.json")
    inputs.file("package-lock.json")

    /*doLast {
         file("build/resources/main/assets/index.html").renameTo(file("build/resources/main/index.html"))
    }*/
}
webpack_react4xp_components.dependsOn += 'config_react4xp'

jar.dependsOn += 'webpack_react4xp_core'
jar.dependsOn += 'webpack_react4xp_components'


// processes your package.json before running webpack
jar.dependsOn babelXP

jar.outputs.dir('build/libs')
